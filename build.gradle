plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
}

base {
	archivesName.set(project.archives_base_name)
}

version = "$project.version+${libs.versions.minecraft.get()}"
group = project.maven_group

repositories {

}

dependencies {
	minecraft libs.minecraft
	mappings variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' }
	modImplementation libs.quilt.loader

	// We only need to includes the getter because we look it up by class name
	include(project(":InstrumentationGetterSubproject"))
	// this lets us dynamically attach the agent at runtime and handles all the ugly native stuff
	implementation("net.bytebuddy:byte-buddy-agent:1.14.7")
	include("net.bytebuddy:byte-buddy-agent:1.14.7")
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand 'version': version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release.set(17)
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from('LICENSE') {
		rename { "${it}_${base.archivesName.get()}" }
	}
}
